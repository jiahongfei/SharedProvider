// 根节点添加
def package_version = versions.name  // JCenter的VersionId
def siteUrl = 'https://github.com/jiahongfei/SharedProvider'    // project homepage
def gitUrl = 'https://github.com/jiahongfei/SharedProvider.git' // project git
def project_name = 'SharedProvider'
def package_repo = 'maven' // bintray上的仓库名，一般为maven
def package_type = 'aar'  // 输出类型
def package_group = 'com.andrjhf.sharedprovider' // JCenter的GroupId
def package_artifact = 'sharedprovider-library' // JCenter的ArtifactId
def package_description = 'SharedProvider'
//开发者信息
def developerId = 'jiahongfei'
def developerName = 'jiahongfei'
def developerEmail = 'jiahongfeinew@163.com'
//开源协议
def licenseName = 'The Apache Software License, Version 2.0'
def licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
def allLicenses = ["Apache-2.0"]



// 根节点添加
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = package_version
group = package_group

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 根节点添加
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging = package_type
                name = project_name
                url  = siteUrl
                // Set your license
                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }
                // 根节点添加
                groupId  = package_group
                artifactId = package_artifact

                developers {
                    developer { //填写的一些基本信息
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }
}

//配置bintray参数
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = package_repo               //跟上面创建的Maven仓库名字保持一致
        name = project_name                //发布到JCenter上的项目名字
        desc = package_description
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
    }
}